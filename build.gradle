plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id "org.sonarqube" version "4.4.1.3373"
}

group = 'com.myreflectionthoughts'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required = true
		html.required = true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'com/myreflectionthoughts/covidstat/config/**',
					'com/myreflectionthoughts/covidstat/constant/**',
					'com/myreflectionthoughts/covidstat/entity/**',
					'com/myreflectionthoughts/covidstat/exception/**',
					'com/myreflectionthoughts/covidstat/handler/**',
					'com/myreflectionthoughts/covidstat/CovidstatApplication.*',
			])
		}))
	}
}

sonarqube {
	properties {
		property "sonar.exclusions", """
            **/config/**,
            **/constant/**,
            **/entity/**,
            **/exception/**,
            **/handler/**',
            CovidstatApplication.*
        """
		property "sonar.projectKey", "Covidstat-be"
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.coverage.jacoco.xmlReportPaths",
				"$buildDir/reports/jacoco/test/jacocoTestReport.xml"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation  'org.springframework.boot:spring-boot-starter-web'
	implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11")
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compileOnly 'org.projectlombok:lombok:1.18.42'
	annotationProcessor 'org.projectlombok:lombok:1.18.42'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'redis.clients:jedis:7.2.1'

}


tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('buildAndTagDockerImage', Exec) {
	commandLine 'docker', 'build',
			'-t', "covidstat:0.0.1", '.'

	workingDir '.'
	dependsOn(tasks.named('jar'))
}